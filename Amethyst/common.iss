; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!
; Non-commercial use only

#define AmethystName "Amethyst"
#define AmethystVersion ReadIni(SourcePath + "..\metadata.ini", "Amethyst", "Version")
#define AmethystPublisher "kinderhead"
#define AmethystURL "https://www.amethyst.dev/"
#define AmethystExeName "amethyst.exe"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{B74A2A72-2AC3-422E-AC70-6B5769D3D728}}
AppName={#AmethystName}
AppVersion={#AmethystVersion}
AppVerName={#AmethystName} {#AmethystVersion}
AppPublisher={#AmethystPublisher}
AppPublisherURL={#AmethystURL}
AppSupportURL={#AmethystURL}
AppUpdatesURL={#AmethystURL}
DefaultDirName={autopf}\{#AmethystName}
UninstallDisplayIcon={app}\{#AmethystExeName}
DefaultGroupName={#AmethystName}
DisableProgramGroupPage=yes
LicenseFile=..\LICENSE
; Uncomment the following line to run in non administrative install mode (install for current user only).
;PrivilegesRequired=lowest
PrivilegesRequiredOverridesAllowed=dialog
SolidCompression=yes
WizardStyle=modern dynamic
ChangesEnvironment=yes

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Files]
Source: "dist\{#AmethystExeName}"; DestDir: "{app}"; Flags: ignoreversion
Source: "dist\std\*"; DestDir: "{app}\std"; Flags: ignoreversion recursesubdirs
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Tasks]
Name: envPath; Description: "Add application directory to the system PATH";  Flags: checkedonce

[Code]
procedure EnvAddPath(PathToAdd: string);
var
  EnvVar: string;
  RegKey: Integer;
  HivePath: string;
begin
  if IsAdminInstallMode() then begin
    RegKey := HKEY_LOCAL_MACHINE;
    HivePath := 'SYSTEM\CurrentControlSet\Control\Session Manager\Environment';
  end else begin
    RegKey := HKEY_CURRENT_USER;
    HivePath := 'Environment';
  end;

  if RegQueryStringValue(RegKey, HivePath, 'Path', EnvVar) then
  begin
    if Pos(';' + UpperCase(PathToAdd) + ';', UpperCase(';' + EnvVar + ';')) = 0 then
    begin
      EnvVar := EnvVar + ';' + PathToAdd;
      RegWriteStringValue(RegKey, HivePath, 'Path', EnvVar);
    end;
  end
  else
  begin
    RegWriteStringValue(RegKey, HivePath, 'Path', PathToAdd);
  end;
end;

procedure EnvRemovePath(PathToRemove: string);
var
  EnvVar, NewEnvVar: string;
  RegKey: Integer;
  HivePath: string;
begin
  RegKey := HKEY_LOCAL_MACHINE;
  HivePath := 'SYSTEM\CurrentControlSet\Control\Session Manager\Environment';
  if RegQueryStringValue(RegKey, HivePath, 'Path', EnvVar) then
  begin
    NewEnvVar := EnvVar;
    StringChangeEx(NewEnvVar, PathToRemove + ';', '', True);
    StringChangeEx(NewEnvVar, ';' + PathToRemove, '', True);
    if NewEnvVar = PathToRemove then NewEnvVar := '';
    RegWriteStringValue(RegKey, HivePath, 'Path', NewEnvVar);
  end;

  RegKey := HKEY_CURRENT_USER;
  HivePath := 'Environment';
  if RegQueryStringValue(RegKey, HivePath, 'Path', EnvVar) then
  begin
    NewEnvVar := EnvVar;
    StringChangeEx(NewEnvVar, PathToRemove + ';', '', True);
    StringChangeEx(NewEnvVar, ';' + PathToRemove, '', True);
    if NewEnvVar = PathToRemove then NewEnvVar := '';
    RegWriteStringValue(RegKey, HivePath, 'Path', NewEnvVar);
  end;
end;

procedure CurStepChanged(CurStep: TSetupStep);
begin
  if CurStep = ssPostInstall then
  begin
    if WizardIsTaskSelected('envPath') then
      EnvAddPath(ExpandConstant('{app}'));
  end;
end;

procedure CurUninstallStepChanged(CurUninstallStep: TUninstallStep);
begin
  if CurUninstallStep = usPostUninstall then
  begin
    EnvRemovePath(ExpandConstant('{app}'));
  end;
end;

[Icons]
Name: "{group}\{#AmethystName}"; Filename: "{app}\{#AmethystExeName}"

[UninstallDelete]
Type: files; Name: "{app}\std\*.*"
Type: dirifempty; Name: "{app}\std"
