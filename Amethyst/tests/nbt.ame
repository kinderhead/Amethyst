namespace test;

struct vec {
    int x;
    int y;
    int z;

    overload int sum() {
        return this.x + this.y + this.z;
    }

    overload int sum(int mod) {
        return this.x + this.y + this.z + mod;
    }
}

struct vec2 {
    int x;
    int y;
    int z;

    vec2(int x, int y, int z) {
        this.x = x;
        this.y = y;
        this.z = z;
    }

    int sum() {
        return this.x + this.y + this.z;
    }

    virtual int get-x() {
        return this.x;
    }
}

struct subvec implements vec2 {
    subvec(int x, int y, int z) : vec2(x * 2, y * 2, z * 2) { }

    virtual int get-x() {
        return this.x * 2;
    }
}

struct vec3 {
    int x;
    int y;
    int z;

    vec3 add(macro vec3& b) {
        return { x: this.x + b.x, y: this.y + b.y, z: this.z + b.z };
    }
}

#test
void test_nbt() {
    start("nbt");
    
    nbt obj;
    obj.x = 100;
    assert_eq((int)obj.x, 100);

    complete();
}

#test
void test_normal() {
    start("nbt normal");
    
    vec obj = { x: 100 };
    obj.y = 7;

    assert_eq(obj.x, 100);
    assert_eq(obj.y, 7);
    assert_eq(obj.z, 0);
    assert_eq(obj.sum(), 107);
    assert_eq(obj.sum(3), 110);

    complete();
}

#test
void test_constructor() {
    start("nbt constructor");
    
    vec2 obj = vec2(3, 6, 4);

    assert_eq(obj.x, 3);
    assert_eq(obj.y, 6);
    assert_eq(obj.z, 4);
    assert_eq(obj.sum(), 13);
    assert_eq(obj.get-x(), 3);

    complete();
}

#test
void test_inheritance() {
    start("nbt inheritance");
    
    subvec obj = subvec(1, 2, 3);

    assert_eq(obj.x, 2);
    assert_eq(obj.y, 4);
    assert_eq(obj.z, 6);
    assert_eq(obj.sum(), 12);
    assert_eq(obj.get-x(), 4);

    complete();
}

#test
void test_nbt_literal() {
    start("nbt literal");
    
    nbt obj = { x: 100 };
    assert_eq((int)obj.x, 100);

    complete();
}

#test
void test_nbt_fancy_literal() {
    start("nbt fancy literal");
    
    vec3 a = { x: 1, y: 2, z: 3 };
    vec3 b = { x: 10, y: 20, z: 30 };
    vec3 c = a.add(b);

    assert_eq(c.x, 11);
    assert_eq(c.y, 22);
    assert_eq(c.z, 33);

    complete();
}
